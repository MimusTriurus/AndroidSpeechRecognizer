apply plugin: 'com.android.library'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'
}
// удаляем предыдущую версию библиотеки
task deleteOldJar(type: Delete){
    delete '../build/release/SpeechRecognizer.jar'
}
// экспортируем целевую библиотеку в каталог сборки
task exportJar(type: Copy){
    from('build/intermediates/bundles/release/')
    into('../build/release/')
    include('classes.jar')
    rename('classes.jar', 'SpeechRecognizer.jar')
}
// копируем файлы второй очереди в каталог сборки
task copyDependencies {
    doLast {
        copy {
            from 'build/intermediates/bundles/release/libs/pocketsphinx-android-5prealpha-nolib.jar'
            into '../build/release/'
        }

        copy {
            from 'build/intermediates/sourceFolderJavaResources/release/lib/armeabi/libpocketsphinx_jni.so'
            into '../build/release/libs/armeabi-v7a/'
        }

        copy {
            from 'build/intermediates/bundles/release/AndroidManifest.xml'
            into '../build/release/'
        }
    }
}
// порядок выполнения задачи экспорта
exportJar.dependsOn(deleteOldJar, build, copyDependencies)
